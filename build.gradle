plugins {
  id "groovy"
  id "java"
  id 'maven-publish'
  id 'com.gradle.plugin-publish' version '0.9.9'
  id "com.vanniktech.android.junit.jacoco" version "0.11.0"
  id "fr.coppernic.versioning" version "3.1.2"
}

repositories {
  jcenter()
  google()
}

dependencies {
  compile gradleApi()
  compile localGroovy()

  compileOnly 'com.android.tools.build:gradle:3.1.2'

  testCompile 'com.android.tools.build:gradle:3.1.2'
  testCompile 'junit:junit:4.12'
}

sourceCompatibility = JavaVersion.VERSION_1_7

group = GROUP
version = versioning.info.display

pluginBundle {
  website = POM_URL
  vcsUrl = POM_SCM_URL

  plugins {
    androidJavadoc {
      id = 'fr.coppernic.android.javadoc'
      displayName = POM_NAME
      tags = ['gradle', 'android', 'javadoc', 'app module', 'library module']
      description = POM_DESCRIPTION
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.5'
  //noinspection UnnecessaryQualifiedReference
  distributionType = Wrapper.DistributionType.ALL
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
  classifier = 'groovydoc'
  from 'build/docs/groovydoc'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

test {
  environment "ANDROID_HOME", System.getenv("ANDROID_HOME")
  testLogging {
    showStackTraces true
    showStandardStreams = true
    events "failed"
    exceptionFormat "full"
  }
}

// Using :publishToMavenLocal will put the jar in $home/.m2 repo
publishing {
  publications {
    copper(MavenPublication) {
      groupId = "fr.coppernic.gradle.plugin"
      artifactId = POM_ARTIFACT_ID

      //artifact jar
      from components.java
    }
  }
}

publishToMavenLocal.dependsOn build
publishPlugins.dependsOn build
